HAI 1.2
  OBTW, DIS PROGRAM R T3H CUDDLIEST CUZ IT R WRITTEN IN LOLCODE! =^_^=
  RUN DIS WIZ LOLCODE VERSHUN 1.2 COMPLIANT INTERPRETARS LIEK http://lolcode.org/
  ALSO ALSO DIS NEEDS T3H PREPROSESHING CUZ LOLCODE DUN LIEK SPACES. FO EXZAMPAL:
  tr [:space:] "\n" < input.in | lci LAWNMOWAH.LOL > out
  TLDR, I HAS A LAWNZTEHDO, GIMMEH LAWNZTEHDO, LAWNZTEHDO IS NOW A NUMBR
  IM IN YR LAWNMOWAR UPPIN YR LAWNNUMBR TIL BOTH SAEM LAWNNUMBR AN LAWNZTEHDO
    I HAS A TALLNEZZ, GIMMEH TALLNEZZ, TALLNEZZ IS NOW A NUMBR
    I HAS A WIDENEZZ, GIMMEH WIDENEZZ, WIDENEZZ IS NOW A NUMBR
    I HAS A LAWN ITZ A BUKKIT, I HAS A SAVEROW ITZ A BUKKIT, I HAS A SAVECOL ITZ A BUKKIT
    BTW NOW I EXPLOR TEH LAWNZ TO LOOKS AT TEH GRASS'Z HITE
    IM IN YR ROWEXPLORR UPPIN YR ROWZ TIL BOTH SAEM ROWZ AN TALLNEZZ
      LAWN HAS A SRS ROWZ ITZ A BUKKIT
      SAVEROW HAS A SRS ROWZ ITZ A TROOF
      IM IN YR COLEXPLORR UPPIN YR COLZ TIL BOTH SAEM COLZ AN WIDENEZZ
        I HAS A NEXTHEIGHT, GIMMEH NEXTHEIGHT, NEXTHEIGHT IS NOW A NUMBR
        LAWN'Z SRS ROWZ HAS A SRS COLZ ITZ NEXTHEIGHT
      IM OUTTA YR COLEXPLORR
    IM OUTTA YR ROWEXPLORR
    BTW DIS MAEKS TEH SAVECOL BUKKIT
    IM IN YR COLEXPLORR UPPIN YR COLZ TIL BOTH SAEM COLZ AN WIDENEZZ
      SAVECOL HAS A SRS COLZ ITZ A TROOF
    IM OUTTA YR COLEXPLORR  
    BTW DIS CHEKS IF EACH ROW AND COL CAN B CUTS
    IM IN YR ROWEXPLORR UPPIN YR ROWZ TIL BOTH SAEM ROWZ AN TALLNEZZ
      IM IN YR COLEXPLORR UPPIN YR COLZ TIL BOTH SAEM COLZ AN WIDENEZZ
        BOTH SAEM 2 AN LAWN'Z SRS SUM OF ROWZ AN 0'Z SRS COLZ, O RLY?
          YA RLY, SAVEROW'Z SRS ROWZ R WIN, SAVECOL'Z SRS COLZ R WIN, OIC
      IM OUTTA YR COLEXPLORR
    IM OUTTA YR ROWEXPLORR
    OBTW, NOW LOLCATZ DECIDES IF LOLCATZ CAN MAEK TEH LAWN PATERNS. DUN WORRY, IT R TAKE 
    NOT VERY LONG BECUZ I HAZ ARMY OF LOLKITTEH! =^_^=   =^_^=   =^_^=   =^_^=   =^_^=
    TLDR, I HAS A CANMAEKZ ITZ WIN 
    IM IN YR ROWCHECKR UPPIN YR ROWZ TIL BOTH SAEM ROWZ AN TALLNEZZ
      IM IN YR COLCHECKR UPPIN YR COLZ TIL BOTH SAEM COLZ AN WIDENEZZ
      ALL OF BOTH SAEM 1 AN LAWN'Z SRS SUM OF ROWZ AN 0'Z SRS COLZ AN SAVEROW'Z SRS ROWZ AN SAVECOL'Z SRS COLZ MKAY, O RLY?
      YA RLY, CANMAEKZ R FAIL, OIC
      IM OUTTA YR COLCHECKR
    IM OUTTA YR ROWCHECKR
    OBTW NOW I IZ HAPPY CUZ CAN REPORTS TO TEH RESULTZ!
        /\__/\
       /`    '\
     === ^  ^ ===
       \  --  /
      /        \
     /          \
    |            |
     \  ||  ||  /
      \_oo__oo_/#######o
    TLDR, I HAS A REPORTS ITZ A YARN
    CANMAEKZ, O RLY? 
        YA RLY, REPORTS R "YES"
        NO WAI, REPORTS R "NO"
      OIC
    VISIBLE SMOOSH "Case #" SUM OF LAWNNUMBR AN 1 ": " REPORTS MKAY
  IM OUTTA YR LAWNMOWAR
KTHXBYE
